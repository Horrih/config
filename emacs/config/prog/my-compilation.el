;;; Compilation misc -*- lexical-binding: t; -*-
(use-package compile
  :straight (:type built-in)
  :hook (compilation-mode . (lambda()(setq show-trailing-whitespace nil)))
  :custom
  (compilation-max-output-line-length 10000) ; Disable the default 400char line wrapping
  (compilation-always-kill t)) ; Do not ask for confirmation when I stop current compilation

;;; Hydra compile
(transient-define-prefix my/transient-compile()
  "Compilation command selecter"
  [["Compilation basic"
    ("e" "Edit compile command" compile)
    ("รง" "Recompile" my/recompile-switch)
    ("k" "Stop compilation" kill-compilation)]
   ["Testing/linting"
    ("l" "Lint this project" my/compile-lint-project)
    ("p" "Run this project's tests" my/compile-test-project)
    ("f" "Run this buffer's tests" my/compile-test-file)
    ("c" "Run this test case" my/compile-test-case)
    ("g" "Start gdb" my/transient-gdb)
    ]
   ["Buffer commands"
    ("o" "Open *compilation* buffer"  my/switch-to-compilation-other-window)
    ("L" "Cycle error levels"         compilation-set-skip-threshold :transient t)
    ("N" "Next compilation error"     next-error                     :transient t)
    ("P" "Previous compilation error" previous-error                 :transient t)
    ]])

;;; Compilation helper functions

;;;; my/switch-to-compilation-other-window()
(defun my/switch-to-compilation-other-window()
  "Switches to the compilation buffer in another window"
  (interactive)
  (unless (string-equal "*compilation*" (buffer-name))
    (switch-to-buffer-other-window "*compilation*")))

(defun my/switch-to-compilation-other-window-end()
  "Switches to the compilation buffer in another window and go to buffer end"
  (interactive)
  (my/switch-to-compilation-other-window)
  (end-of-buffer))

;;;; my/recompile-switch
(defun my/recompile-switch()
  "Uses the recompile function and switches to the buffer end"
  (interactive)
  (recompile)
  (my/switch-to-compilation-other-window-end))

;;;; my/compile-test-project
(defun my/compile-test-project()
  "Runs the tests contained in the current project."
  (interactive)
  (my/compile--command-runner "project tests" 'my/compile-test-project-command))


;;;; my/compile-test-file
(defun my/compile-test-file()
  "Runs the tests contained in the current buffer."
  (interactive)
  (my/compile--command-runner "single file tests" 'my/compile-test-file-command))


;;;; my/compile-test-case
(defun my/compile-test-case()
  "Runs the test case where the cursor is located."
  (interactive)
  (my/compile--command-runner "single test case" 'my/compile-test-case-command))

;;;; my/compile-lint
(defun my/compile-lint-project()
  "Runs the linters for the current project."
  (interactive)
  (my/compile--command-runner "project lint" 'my/compile-lint-project-command))

;;;; Helper function to run a compilation command defined in a symbol
(defun my/compile--command-runner(descr command-callback)
  "Runs the tests returned by COMMAND-CALLBACK in a compilation window.

COMMAND-CALLBACK is a symbol containing a function that muse return:
- The directory where this command should be run
- The command to run"
  (when (derived-mode-p 'compilation-mode)
    (quit-window))
  (unless (boundp command-callback)
    (error (concat (format "ERROR : The %s command to run is unknown.\n" descr)
                   (format "Please define `%s' for the current major mode." command-callback))))
  (pcase-let ((`(,default-directory ,command) (funcall (symbol-value command-callback))))
    (compile command))
  (my/switch-to-compilation-other-window-end))

;;; ansi-color : Translate TTY escape sequences into colors
(defun my/ansi-color-compilation-filter-except-ag()
  "Like `ansi-color-compilation-filter', except on buffers generated by the ag package.
   If we use vanilla ansi-color-compilation-filter, the colors get messed up"
  (unless (string-match "ag search text" (buffer-name))
    (ansi-color-compilation-filter)))

(use-package ansi-color
  :straight (:type built-in)
  :hook (compilation-filter . my/ansi-color-compilation-filter-except-ag)) ; Handle terminal colors in the compilation buffer

;;; Error regexps : Set compilation regex for errors
(let ((enabled-regexps ())
      (custom-error-list '(
                           ;; Insert your custom regexps here
                           (file-path "^\\([0-9a-zA-Z_/.]*\\..*\\):\\([0-9]+\\):.*$" 1 2 nil 0)
                           (file-path-col "^\\([0-9a-zA-Z_/\\.]*\\..*\\):\\([0-9]+\\):\\([0-9]+\\):.*$" 1 2 3 0)
                           (doctest "^\\(.*\\):\\([0-9]+\\): FATAL ERROR" 1 2 nil)
                           (doxygen "^\\(.*\\):\\([0-9]+\\): warning: " 1 2 nil 1)
                           (cppcheck "^\\(.*\\..*\\):\\([0-9]+\\):\\([0-9]+\\):.*\\[.*\\]$" 1 2 3 1)
                           (link-error "^\\(.*\\):.*: undefined reference to `\\(.*\\)'$" 1 nil nil nil nil (2 compilation-info-face))
                           (jest-error "^.*\(\\(.*\\):\\([0-9]+\\):\\([0-9]+\\)\).*$" 1 2 3)
                           (gcc-error "^[ ]*\\(.*\\):\\([0-9]+\\):\\([0-9]+\\): \\(.*error:\\|  required from here\\).*$" 1 2 3)
                           (gcc-warning "^\\(.*\\):\\([0-9]+\\):\\([0-9]+\\): warning:.*$" 1 2 3 1)
                           (gcc-info "^\\(.*\\):\\([0-9]+\\):\\([0-9]+\\): note:.*$" 1 2 3 0)
                           (qt-test "^   Loc: \\[\\(.*\\)\(\\([0-9]+\\)\)\\]$" 1 2)
                           (python-unittest "^  File \"\\(.*\\)\", line \\([0-9]+\\)[^0-9]?.*$" 1 2)
                           (pylint "^\\([0-9a-zA-Z_/.]*\\..*\\):\\([0-9]+\\):\\([0-9]+\\): [a-zA-Z][0-9]*: .*$" 1 2 3 1)
                           )))
  (dolist (err custom-error-list)
    (add-to-list 'enabled-regexps (car err)))
  (custom-set-variables `(compilation-error-regexp-alist ',enabled-regexps))
  (add-hook 'compilation-mode-hook (lambda()
                                     (dolist (err custom-error-list)
                                       (add-to-list 'compilation-error-regexp-alist-alist err)))))
